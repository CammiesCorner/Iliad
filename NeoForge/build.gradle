plugins {
    alias libs.plugins.moddevgradle
}

// need this before dependencies because it configures the plugin and creates additionalRuntimeClasspath configuration
neoForge.version = neoforge.versions.neoforge.get()

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    compileOnly project(":Common")

    implementation libs.sparkweave.neoforge
    implementation libs.resourcefulconfig.neoforge
    implementation libs.common.network.neoforge

    compileOnly variantOf(libs.emi.neoforge) { classifier 'api' }
    localRuntime libs.emi.neoforge
}

neoForge {
    parchment {
        minecraftVersion = libs.versions.parchment.minecraft.get()
        mappingsVersion = libs.versions.parchment.mappings.get()
    }

    accessTransformers {
        def atFile = project(":Common").file("src/main/resources/META-INF/accesstransformer.cfg")
        from atFile
        publish atFile
    }
    validateAccessTransformers.set(true)

    interfaceInjectionData {
        def itfFile = project(":Common").file("interfaces.json")
        from itfFile
        publish itfFile
    }

    mods {
        "${project.mod_id}" {
            sourceSet sourceSets.main
            sourceSet project(":Common").sourceSets.main
        }
    }

    runs {
        configureEach {
            logLevel = org.slf4j.event.Level.DEBUG

            systemProperty 'forge.logging.markers', 'REGISTRIES'
            systemProperty 'sparkweave.debug', 'true'

            gameDirectory = project.file('run')

            if (rootProject.hasProperty('mc_java_agent_path')) {
                jvmArgument "-javaagent:${rootProject.mc_java_agent_path}"
            }
        }

        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id

            if (rootProject.hasProperty('mc_uuid')) {
                programArgument "--uuid=${rootProject.mc_uuid}"
            }

            if (rootProject.hasProperty('mc_username')) {
                programArgument "--username=${rootProject.mc_username}"
            }
        }

        server {
            server()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
            programArgument '--nogui'
        }

        data {
            data()
            programArguments.addAll('--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').absolutePath, '--existing', file('src/main/resources/').absolutePath)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.named("compileJava", JavaCompile) {
    source project(":Common").sourceSets.main.allJava
}

tasks.named("javadoc", Javadoc) {
    source project(":Common").sourceSets.main.allJava
}

tasks.named("sourcesJar", Jar) {
    from project(":Common").sourceSets.main.allSource
}

processResources {
    from project(":Common").sourceSets.main.resources
}
